targets=(
    "archlinux"
    "fedora"
    "opensuse"
    "ubuntu"
)
pkgname="cie-middleware"
pkgver="1.4.2"
pkgrel="1"
pkgdesc="A fork of Middleware della CIE (Carta di Identità Elettronica) per Linux"
pkgdesclong=("A fork of Middleware della CIE (Carta di Identità Elettronica) per Linux")
maintainer="boiano.gianluca@gmail.com"
url="https://github.com/italia/cie-middleware-linux"
arch="amd64"
license=("BSD-3")
section="utils"
priority="optional"
sources=(
    "https://github.com/M0Rf30/cie-middleware-linux/archive/main.tar.gz"
    "https://github.com/M0Rf30/cie-middleware-linux/releases/download/podofo-1.4.2/libpodofo-1.4.2.tar.gz"
    "https://downloads.gradle-dn.com/distributions/gradle-7.3-bin.zip"
)
sources:archlinux=(
    "https://github.com/M0Rf30/cie-middleware-linux/archive/main.tar.gz"
    "https://github.com/M0Rf30/cie-middleware-linux/releases/download/podofo-1.4.2/libpodofo-1.4.2.tar.gz"
)

hashsums=(
    "skip"
    "ef81e439b12aa2ff309b353399c033bd62b027e7bd4cbf649c025226f1de5287"
    "skip"
)
hashsums:archlinux=(
    "skip"
    "ef81e439b12aa2ff309b353399c033bd62b027e7bd4cbf649c025226f1de5287"
)
depends:apt=(
    "cryptopp"
    "libcurl4"
    "libfreetype6"
    "libfontconfig1"
    "libpcsclite1"
    "libpng16-16"
    "libssl1.1"
    "libxml2"
    "openjdk-11-jre-headless"
    "pcscd"
)
makedepends:apt=(
    "ccache"
    "g++"  
    "libcurl4-openssl-dev"
    "libfontconfig1-dev"
    "libfreetype6-dev"
    "libpcsclite-dev"
    "libpng-dev"
    "libssl-dev"
    "libxml2-dev"
    "openjdk-11-jre-headless"
    "pkg-config"
    "python3-pip"
    "unzip"
)

depends:pacman=(
    "crypto++"
    "curl"
    "fontconfig"
    "freetype2"
    "java-runtime"
    "libpng"
    "libxml2"
    "openssl"
    "pcsclite"
)
makedepends:pacman=(
    "ccache"
    "crypto++"
    "curl"
    "fontconfig"
    "freetype2"
    "gradle"
    "libpng"
    "libxml2"
    "meson"
    "openssl"
    "pcsclite"
    "pkgconf"
    "unzip"
)

depends:yum=(
    "cryptopp"
    "fontconfig"
    "freetype"
    "java-11-openjdk-headless"
    "libpng"
    "libxml2"
    "openssl"
    "pcsc-lite"
)
makedepends:yum=(
    "ccache"
    "cryptopp-devel"
    "fontconfig-devel"
    "freetype-devel"
    "gcc-c++"
    "java-11-openjdk-devel"
    "libpng-devel"
    "libxml2-devel"
    "meson"
    "openssl-devel"
    "pcsc-lite-devel"
    "pkgconf-pkg-config"
)

build:archlinux() {
    export JAVA_HOME=/usr/lib/jvm/default/
    pip3 install ninja==1.10.2.3 meson==0.61.1
    cp "${srcdir}/libpodofo.a" \
      "${srcdir}/${pkgname}-linux-main/libs/lib"

    cd "${srcdir}/${pkgname}-linux-main"
    gradle -b cie-java/build.gradle standalone
    meson builddir libs
    meson configure -Dprefix=/usr builddir
    meson compile -C builddir
}

build:fedora() {
    export JAVA_HOME=/usr/lib/jvm/java-11-openjdk/
    cp "${srcdir}/libpodofo.a" \
      "${srcdir}/${pkgname}-linux-main/libs/lib"

    cd "${srcdir}/${pkgname}-linux-main"
    ../gradle-7.3/bin/gradle -b cie-java/build.gradle standalone
    meson builddir libs
    meson configure -Dprefix=/usr builddir
    meson compile -C builddir
}

build:ubuntu() {
    export JAVA_HOME=/usr/lib/jvm/java-11-openjdk-amd64
    pip3 install ninja==1.10.2.3 meson==0.61.1
    cp "${srcdir}/libpodofo.a" \
      "${srcdir}/${pkgname}-linux-main/libs/lib"

    cd "${srcdir}/${pkgname}-linux-main"
    ../gradle-7.3/bin/gradle -b cie-java/build.gradle standalone

    meson builddir libs
    meson configure -Dprefix=/usr builddir
    meson compile -C builddir
}

package() {
    cd "${srcdir}/${pkgname}-linux-main"
    
    # Java Application
    install -Dm755 cie-java/bin/libs/CIEID-standalone.jar \
    "${pkgdir}/usr/share/cieid/cieid.jar"
    install -Dm644 "data/cieid.desktop" \
    "${pkgdir}/usr/share/applications/cieid.desktop"
    install -Dm755 data/logo.png \
    "${pkgdir}/usr/share/pixmaps/cieid.png"    
    install -Dm755 "data/cieid.sh" \
    "${pkgdir}/usr/bin/cieid"
    install -Dm644 LICENSE \
    "${pkgdir}/usr/share/licenses/cieid/LICENSE"

    # Lib
    DESTDIR="${pkgdir}" meson install -C builddir
}
