pkgname="cie-middleware"
pkgver="1.5.8"
pkgrel="1"
pkgdesc="A fork of Middleware della CIE (Carta di Identit√† Elettronica) per Linux"
maintainer="M0Rf30"
url="https://github.com/italia/cie-middleware-linux"
arch=("x86_64")
license=("BSD-3-Clause")
section="utils"
priority="optional"
source=(
  "https://github.com/M0Rf30/cie-middleware-linux/archive/refs/tags/${pkgver}.tar.gz"
)
sha256sums=(
  "SKIP"
)

depends__apt=(
  "libcrypto++8"
  "libcurl4"
  "libfreetype6"
  "libfontconfig1"
  "libpcsclite1"
  "libpng16-16"
  "libpodofo-utils"
  "libssl3"
  "libxml2"
  "openjdk-17-jre-headless"
  "pcscd"
)
makedepends__apt=(
  "g++"
  "libcrypto++-dev"
  "libcurl4-openssl-dev"
  "libfontconfig1-dev"
  "libfreetype6-dev"
  "libpcsclite-dev"
  "libpng-dev"
  "libpodofo-dev"
  "libssl-dev"
  "libxml2-dev"
  "openjdk-17-jdk-headless"
  "pkg-config"
  "python3-pip"
  "unzip"
)

depends__pacman=(
  "crypto++"
  "curl"
  "fontconfig"
  "freetype2"
  "jre17-openjdk"
  "libpng"
  "libxml2"
  "openssl"
  "pcsclite"
  "podofo"
)
makedepends__pacman=(
  "crypto++"
  "curl"
  "fontconfig"
  "freetype2"
  "jdk17-openjdk"
  "libpng"
  "libxml2"
  "meson"
  "ninja"
  "openssl"
  "pcsclite"
  "pkgconf"
  "unzip"
)

depends__yum=(
  "cryptopp"
  "fontconfig"
  "freetype"
  "java-17-openjdk-headless"
  "libcurl"
  "libpng"
  "libxml2"
  "openssl"
  "pcsc-lite"
  "podofo-libs"
)
makedepends__yum=(
  "cryptopp-devel"
  "fontconfig-devel"
  "freetype-devel"
  "gcc-c++"
  "java-17-openjdk-devel"
  "libcurl-devel"
  "libpng-devel"
  "libxml2-devel"
  "meson"
  "openssl-devel"
  "pcsc-lite-devel"
  "pkgconf-pkg-config"
  "podofo-devel"
)

build__pacman() {
  export JAVA_HOME=/usr/lib/jvm/default/

  cd "${srcdir}/${pkgname}-linux-${pkgver}"
  cie-java/gradlew -b cie-java/build.gradle standalone
  meson setup builddir libs
  meson configure -Dprefix=/usr builddir
  meson compile -C builddir
}

build__yum() {
  export JAVA_HOME=/usr/lib/jvm/java-17-openjdk/

  cd "${srcdir}/${pkgname}-linux-${pkgver}"
  cie-java/gradlew -b cie-java/build.gradle standalone
  meson setup builddir libs
  meson configure -Dprefix=/usr builddir
  meson compile -C builddir
}

build__apt() {
  export JAVA_HOME=/usr/lib/jvm/java-17-openjdk-amd64
  pip3 install ninja==1.10.2.3 meson==0.61.1

  cd "${srcdir}/${pkgname}-linux-${pkgver}"
  cie-java/gradlew -b cie-java/build.gradle standalone

  meson setup builddir libs
  meson configure -Dprefix=/usr builddir
  meson compile -C builddir
}

package() {
  cd "${srcdir}/${pkgname}-linux-${pkgver}"

  # Java Application
  install -Dm755 cie-java/build/libs/CIEID-standalone.jar \
    "${pkgdir}/usr/share/cieid/cieid.jar"
  install -Dm644 "data/app.m0rf30.cieid.desktop" \
    "${pkgdir}/usr/share/applications/app.m0rf30.cieid.desktop"
  install -Dm755 data/app.m0rf30.cieid.svg \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/app.m0rf30.cieid.svg"
  install -Dm644 data/libcie-pkcs11.module \
    "${pkgdir}/usr/share/p11-kit/modules/libcie-pkcs11.module"
  mkdir -p "${pkgdir}/usr/lib/pkcs11"
  ln -s /usr/lib/libcie-pkcs11.so \
    "${pkgdir}/usr/lib/pkcs11/libcie-pkcs11.so"
  install -Dm755 "data/cieid.sh" \
    "${pkgdir}/usr/bin/cieid"
  install -Dm644 LICENSE \
    "${pkgdir}/usr/share/licenses/cieid/LICENSE"

  # Lib
  DESTDIR="${pkgdir}" meson install -C builddir
}
