plugins {
// Apply the java plugin to add support for Java
	id 'java'

// Apply the application plugin to add support for building a CLI application.
	id 'application'
	id "com.diffplug.spotless" version "7.1.0"
}

repositories {
	mavenCentral()
}

spotless {
	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		leadingSpacesToTabs()
		endWithNewline()
	}
	java {
		// apply a specific flavor of google-java-format
		googleJavaFormat('1.17.0').aosp()
	}
}

dependencies {
	implementation 'commons-io:commons-io:2.19.0'
	implementation 'com.google.code.gson:gson:2.13.1'
	implementation 'net.java.dev.jna:jna:5.17.0'
	implementation 'org.apache.pdfbox:pdfbox:3.0.5'
}

sourceSets {
	main {
		java {
			srcDirs = ['src']
		}
	}
}

tasks.withType(JavaCompile) {
	options.compilerArgs += "-Xlint:deprecation"
	options.encoding = "UTF-8"
}

task standalone(type: Jar) {
	dependsOn configurations.runtimeClasspath
	archiveClassifier = 'standalone'
	from sourceSets.main.output
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	from(configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }) {
		exclude "META-INF/*.SF"
		exclude "META-INF/*.DSA"
		exclude "META-INF/*.RSA"
	}
	manifest {
		attributes 'Implementation-Title': 'CIEID Desktop Application',
				'Built-By': System.getProperty('user.name'),
				'Built-JDK': System.getProperty('java.version'),
				'Main-Class': 'app.m0rf30.cieid.MainApplication'
	}
}

task fetchDeps {
	description = "Force gradle to cache all dependencies"
	doLast { sourceSets.main.runtimeClasspath.files }
}

allprojects {
	tasks.withType(AbstractArchiveTask).configureEach {
		preserveFileTimestamps = false
		reproducibleFileOrder = true
	}
}
