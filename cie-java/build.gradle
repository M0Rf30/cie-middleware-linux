plugins {
// Apply the java plugin to add support for Java
	id 'java'

// Apply the application plugin to add support for building a CLI application.
	id 'application'
	id "com.diffplug.spotless" version "6.18.0"
}

repositories {
	mavenCentral()
}

spotless {
	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	java {
		// apply a specific flavor of google-java-format
		googleJavaFormat('1.14.0').aosp()
	}
}

dependencies {
	// This dependency is used by the application.
	implementation fileTree(dir: 'lib', include: ['*.jar'])
	implementation 'commons-io:commons-io:2.11.0'
	implementation 'com.dorkbox:ObjectPool:4.2'
	implementation 'com.dorkbox:Notify:3.7'
	implementation 'com.dorkbox:ShellExecutor:1.1'
	implementation 'com.dorkbox:SystemTray:4.2.1'
	implementation 'com.dorkbox:TweenEngine:9.0'
	implementation 'com.google.code.gson:gson:2.10.1'
	implementation 'com.jgoodies:jgoodies-forms:1.9.0'
	implementation 'net.java.dev.jna:jna:5.13.0'
	implementation 'net.java.dev.jna:jna-platform:5.13.0'
	implementation 'org.apache.pdfbox:pdfbox:2.0.28'
	implementation 'org.javassist:javassist:3.29.2-GA'
	implementation 'org.slf4j:slf4j-api:2.0.7'
}

mainClassName = 'it.ipzs.cieid.MainApplicationn'

sourceSets {
	main {
		java {
			srcDirs = ['src']
		}
	}
}

tasks.withType(JavaCompile) {
	options.compilerArgs += "-Xlint:deprecation"
	options.encoding = "UTF-8"
}

task standalone(type: Jar) {
	dependsOn configurations.runtimeClasspath
	archiveClassifier = 'standalone'
	from sourceSets.main.output
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	from(configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }) {
		exclude "META-INF/*.SF"
		exclude "META-INF/*.DSA"
		exclude "META-INF/*.RSA"
	}
	manifest {
		attributes 'Implementation-Title': 'CIEID Desktop Application',
				'Built-By': System.getProperty('user.name'),
				'Built-JDK': System.getProperty('java.version'),
				'Main-Class': 'it.ipzs.cieid.MainApplication'
	}
}

task fetchDeps {
	description "Force gradle to cache all dependencies"
	doLast { sourceSets.main.runtimeClasspath.files }
}

allprojects {
	tasks.withType(AbstractArchiveTask).configureEach {
		preserveFileTimestamps = false
		reproducibleFileOrder = true
	}
}
